<html>

<!--
<script src="libs\jquery.js" type="text/javascript"></script>
<script src="libs\three.js" type="text/javascript"></script>-->
<script src ="libs\gl-matrix.js" type ="text/javascript"></script>
<script src="libs\webgl-utils.js" type="text/javascript"></script>

<head>
<title>Water Shader</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE -->

<script id="vs_copy" type="x-shader/x-vertex">
  attribute vec2 position;
  varying vec2 f_Pos;
  void main(void)
  {
        f_Pos = position.xy;
		gl_Position= vec4(position*2.0-vec2(1.0),0,1.0);
  }
</script>
<script id="fs_copy" type="x-shader/x-fragment">
   precision mediump float;
   varying vec2 f_Pos;
   uniform sampler2D uSampler;
   uniform float u_time;

  void main(void)
  {
    vec3 color=texture2D(uSampler,f_Pos).xyz;
	gl_FragColor = vec4(color,1.0);
  }
</script>
<script id="vs_sim" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec2 position;
    varying vec2 f_Pos;
  void main(void)
  {

        f_Pos = position;
		gl_Position= vec4(position,0,1.0);
  }
</script>
<script id="fs_sim" type="x-shader/x-fragment">
   precision mediump float;
   uniform sampler2D uSampler;
   uniform float u_time;
   varying vec2 f_Pos;

  float isValid(vec2 texcoord)
  {
    if(texcoord.x<0.0) return 0.0;
    if(texcoord.y<0.0) return 0.0;
    if(texcoord.x>1.0) return 0.0;
    if(texcoord.y>1.0) return 0.0;
    return 1.0;
  }
  vec2 adjustTexcoord(vec2 texcoord)
  {
    vec2 result = texcoord;
    if(result.x<0.0) result.x=0.0;
    if(result.y<0.0) result.y=0.0;
    if(result.x>1.0) result.x=1.0;
    if(result.y>1.0) result.y=1.0;
    return result;
  }
  float getAverage(sampler2D samp, vec2 texcoord)
  {
    int count = 0;
    float delta=1.0/256.0;
    vec2 upcoord = adjustTexcoord(texcoord + vec2(0,delta));
    vec2 downcoord =adjustTexcoord( texcoord - vec2(0,delta));
    vec2 leftcoord = adjustTexcoord(texcoord + vec2(delta,0));
    vec2 rightcoord =adjustTexcoord( texcoord - vec2(delta,0));
    float totalz=0.0;
    if(isValid(upcoord)>0.5)
    { count+=1; totalz+=texture2D(samp,upcoord).z;}
    if(isValid(downcoord)>0.5)
    { count+=1; totalz+=texture2D(samp,downcoord).z;}
    if(isValid(leftcoord)>0.5)
    { count+=1; totalz+=texture2D(samp,leftcoord).z;}
    if(isValid(rightcoord)>0.5)
    { count+=1; totalz+=texture2D(samp,rightcoord).z;}
    if(count==0) return 0.0;
    return totalz/float(count);
  }
  void main(void)
  {

    vec2 texcoord = (f_Pos+vec2(1.0))*0.5;


    float height=0.0;
    float heightaround=0.0;
    float vel=0.0;
    if(u_time<0.015)
    {
        height = (1.0-length(f_Pos))*1.01;
    }
    else{
        vec2 vel_h=texture2D(uSampler, texcoord).yz;
        height=vel_h.y;
        heightaround =getAverage(uSampler, texcoord);
         vel = vel_h.x;
        vel += (heightaround-height)*0.99;
        height = height+vel;
    }
	gl_FragColor = vec4(0,vel,height,1.0);
  }
</script>
<script id="vs_render" type="x-shader/x-vertex">
precision mediump float;

  attribute vec3 position;
  attribute vec3 normal;

  uniform mat4 u_modelViewPerspective;
  uniform mat4 u_modelViewPerspective_Inverse_Transpose;
  uniform sampler2D uSampler;
  uniform float u_time;
  uniform mat4 u_model;


  varying vec2 heightfieldtexcoord;
  varying float height;
  varying vec3 thenormal;
  varying vec3 position_World;

  void main(void)
  {

		vec2 offsetpos=position.xy*2.0-vec2(1.0);
        heightfieldtexcoord=position.xy;
        float h = texture2D(uSampler,heightfieldtexcoord).z;

        h=position.z;
        gl_Position = u_modelViewPerspective * vec4(vec3(position.xy, h), 1.0);
        height = h;

        //use screen space
        thenormal=normalize(u_modelViewPerspective_Inverse_Transpose*vec4(normalize(normal),0.0)).xyz;
        position_World=(u_modelViewPerspective * vec4(position, 1.0)).xyz;

        //use world space
        thenormal = normalize(normal);
        position_World=(u_model*vec4(position,1.0)).xyz;
  }
</script>
<script id="fs_render" type="x-shader/x-fragment">
    precision mediump float;

	varying vec2 heightfieldtexcoord;

    uniform vec3 eyePos;
    uniform vec3 u_sunPos;
    uniform sampler2D uSampler;
    uniform float u_time;
    uniform mat4 u_modelViewPerspective_Inverse_Transpose;
    uniform mat4 u_modelViewPerspective;


    varying float height;
    varying vec3 thenormal;
    varying vec3 position_World;


    vec3 hdr (vec3 color, float exposure) {
      return 1.0 - exp(-color * exposure);
    }

  void main(void)
  {
  	vec3 outcolor=vec3(1.0);
   vec3 light1=vec3(0.95,0.75,0.45);
    vec3 light2=vec3(1.0);
    vec3 ambientcolor = vec3(93.0,96.0,255.0)/255.0;

    vec3 sunpos = u_sunPos;

    //use screen space
    //sunpos = (u_modelViewPerspective * vec4(sunpos, 1.0)).xyz;

    vec3 incidentdir = normalize(sunpos-position_World);

    //use screen space
    //incidentdir=normalize(u_modelViewPerspective_Inverse_Transpose*vec4(normalize(incidentdir),0.0)).xyz;

    vec3 standardnormal = normalize(thenormal);
    vec3 reflectiondir = normalize(2.0*standardnormal*dot(incidentdir,standardnormal)-incidentdir);

    vec3 eyepos = eyePos;

    //use screen space
    //eyepos = vec3(0,0,0);

    vec3 eyeraydir = normalize(eyepos-position_World);

    vec3 directionalLightDir = normalize(vec3(-2.0,-1.0,-2.0));
    vec3 directLight_Refelction_Dir = normalize(2.0*standardnormal*dot(directionalLightDir,standardnormal)-directionalLightDir);

    //verticalightdir = normalize(sunpos-position_World);
    //use screen space
    //verticalightdir=normalize(u_modelViewPerspective_Inverse_Transpose*vec4(normalize(verticalightdir),0.0)).xyz;

  	float specular1 = pow(max(0.01,-dot(eyeraydir,reflectiondir)),1000.0);
  	float diffuse2 =  pow(max(0.01,-dot(eyeraydir,directLight_Refelction_Dir)),10.0);

  	//float diffuse2 = max(0.0,-dot(standardnormal,verticalightdir));

    //outcolor =3.0*specular1*light1+ 1.0*diffuse2*light2+0.2*ambientcolor;


         vec3 ocean_color = vec3(0.004, 0.016, 0.047);
    vec3 sky_color = vec3(3.2, 9.6, 12.8);

     vec3 view = normalize(eyePos - position_World);

     float incidentAngle=acos(abs(dot(thenormal, view)));
     float transmittanceAngle = asin(sin(incidentAngle)/1.33);      //1.33 is the water transmittance ratio
     float temp1 = tan(incidentAngle-transmittanceAngle)/tan(incidentAngle+transmittanceAngle);
     float temp2 = sin(incidentAngle-transmittanceAngle)/sin(incidentAngle+transmittanceAngle);

     float fresnel =(temp1*temp1+temp2*temp2)*0.5;

     vec3 sky = fresnel * sky_color;

     float diffuse = clamp(dot(thenormal, directionalLightDir), 0.0, 1.0);

     vec3 water = (1.0 - fresnel) * ocean_color * sky_color * diffuse;

     outcolor = 100.0*specular1*light1 + sky + water;


    gl_FragColor = vec4(hdr(outcolor,0.35), 1.0);


    //gl_FragColor = vec4(outcolor, 1.0);
    //gl_FragColor = vec4((thenormal+vec3(1.0))*0.5,1.0);
  }
</script>
<script id="skyboxVS" type="x-shader/x-vertex">
    precision highp float;

    attribute vec3 Position;

    uniform mat4 u_View;
    uniform mat4 u_Persp;

 	varying vec3 v_Texcoord;
    
    void main(void)
    {
        vec4 camera = u_View * vec4(Position, 1.0);
		v_Texcoord = Position;
        gl_Position = u_Persp * camera;
    }
</script>
<script id="skyboxFS" type="x-shader/x-fragment">
    precision highp float;

	uniform samplerCube u_cubeTexture;

    varying vec3 v_Texcoord;
    
    void main(void)
    {
		gl_FragColor = textureCube(u_cubeTexture, v_Texcoord);
    }    
</script>

<script id="skyVS" type="x-shader/x-vertex">
    precision highp float;

    attribute vec2 Position;
 	varying vec2 f_Pos;

    void main(void)
    {
        f_Pos = Position;
		gl_Position= vec4(Position,0.0,1.0);
		//gl_Position= vec4(Position);//*0.1,0,1.0);
    }
</script>
<script id="skyFS" type="x-shader/x-fragment">
precision highp float;

    uniform vec4 sky_params1;
    uniform vec4 sky_params2;
    uniform vec4 sky_params3;
    uniform vec4 sky_params4;
    uniform vec4 sky_params5;
    uniform vec4 sky_params6;

    uniform vec3 eyePos;
    uniform vec3 u_sunPos;
    uniform vec3 eyeCenter;
    uniform vec3 eyeUp;
    uniform float fov;

    varying vec2 f_Pos;

    vec3 calcExtinction(float dist) {
        return exp(dist * sky_params6.xyz);
    }

    vec3 calcScattering(float cos_theta) {
        float r_phase = (cos_theta * cos_theta) * sky_params6.w + sky_params6.w;
        float m_phase = sky_params1.w * pow(sky_params2.w * cos_theta + sky_params3.w, -1.5);
        return sky_params2.xyz * r_phase + (sky_params3.xyz * m_phase);
    }

    float baseOpticalDepth(in vec3 ray) {
        float a1 = sky_params4.x * ray.y;
        return sqrt(a1 * a1 + sky_params4.w) - a1;
    }

    float opticalDepth(in vec3 pos, in vec3 ray) {
        pos.z += sky_params4.x;
        float a0 = sky_params4.y - dot(pos, pos);
        float a1 = dot(pos, ray);
        return sqrt(a1 * a1 + a0) - a1;
    }

    vec3 get_Pixel_Dir(){
        vec3 view = normalize(eyeCenter-eyePos);
        vec3 A= cross(view,eyeUp);
        vec3 B= cross(A,view);
        vec3 M=eyePos+view;
        vec3 V=B*(length(view)*tan(fov)/length(B));
        vec3 H=-A*(length(view)*tan(fov)/length(A));
        float t1=f_Pos.x;
        float t2=f_Pos.y;
        vec3 P=M-t1*H+t2*V;
        vec3 R=normalize(P-eyePos);
        return R;
    }
    void main()
    {
        vec3 view_vec = get_Pixel_Dir();
        vec3 sun_vector = normalize(eyePos-u_sunPos);
        float cos_theta = max(dot(view_vec, sun_vector),0.0);
        float ray_dist = baseOpticalDepth(view_vec);

        vec3 extinction = calcExtinction(ray_dist);
        vec3 light_ray_pos = view_vec * (ray_dist * sky_params4.z);
        float light_ray_dist = opticalDepth(light_ray_pos, sun_vector);
        float light_ray_dist_full = opticalDepth(view_vec * ray_dist, sun_vector);
        light_ray_dist = max(light_ray_dist, light_ray_dist_full);
        vec3 incoming_light = calcExtinction(light_ray_dist*0.01);

        vec3 scattering = calcScattering(cos_theta);
        scattering *= 1.0 - extinction;

        vec3 in_scatter = incoming_light * scattering;

        float sun_strength = clamp(cos_theta * sky_params1.x + sky_params1.y, 0.0, 1.0);
        sun_strength *= sun_strength;
        vec3 sun_disk = extinction * sun_strength;
        gl_FragColor.xyz = sky_params5.xyz * (sky_params5.w * sun_disk + in_scatter);
        gl_FragColor.w = 1.0;
    }
</script>

<script src="water_mesh.js" type="text/javascript"></script>
</head>

<body onload="webGLStart();">

<p style="font-size:30px">This is WebGL water shader</p>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<div id = "debug_text"></div>
<canvas id="canvas1" style="border: none;" width="1024" height="1024" tabindex="1"></canvas>

</body>

</html>
